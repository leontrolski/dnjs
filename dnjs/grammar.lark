dnjs            : statement* [value _NEWLINE]

?statement      : import_
                | export
                | export_default
                | assignment

?unambiguous    : string
                | SIGNED_NUMBER -> number
                | "true"        -> true
                | "false"       -> false
                | "null"        -> null
                | list
                | template
                | ternary_eq
                | bracketed
                | var
                | dot
                | function_call

?value          : unambiguous | dict | function

var             : CNAME
dot             : unambiguous "." var
rest_var        : "..." value
?bracketed      : "(" value ")"
pair            : (string | CNAME) ":" value
?dict_item      : pair  | rest_var
?list_item      : value | rest_var
dict            : "{" [dict_item ("," dict_item)*] [","] "}"
list            : "[" [list_item ("," list_item)*] [","] "]"
ternary_eq      : unambiguous "===" value "?" value ":" value
dict_destruct   : "{" [var ("," var)*] [","] "}"
template_string : /(?:`|}).*?(?:`|\${)/ms
template        : template_string (value template_string)*

import_         : "import" (var | dict_destruct) "from" string _NEWLINE
export          : "export" assignment
export_default  : "export" "default" value _NEWLINE
assignment      : "const" var "=" value _NEWLINE
function        : (var | ("(" [(var | list) ("," var)*] [","] ")")) "=>" unambiguous
function_call   : unambiguous "(" [value ("," value)*] [","] ")"

string          : ESCAPED_STRING
COMMENT         : "//" /.*/
_NEWLINE        : NEWLINE

%import common.LETTER
%import common.DIGIT
%import common.ESCAPED_STRING
%import common.SIGNED_NUMBER
%import common.WS
%import common.NEWLINE
%import common.CNAME
%ignore WS
%ignore COMMENT
